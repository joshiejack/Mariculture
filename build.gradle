buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "gt"
            url = "https://gregtech.overminddl1.com/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url = "https://jitpack.io"
        }
    }
    dependencies {
        classpath 'com.github.GTNewHorizons:ForgeGradle:1.2.11'
    }
}

plugins {
    id 'idea'
}

apply plugin: 'forge'

ext.buildnumber=""
if (System.getenv().BUILD_NUMBER) {
    ext.buildnumber="-" + System.getenv().BUILD_NUMBER
}

version = "${minecraft_version}-${mod_version}${buildnumber}"
group= "uk.joshiejack.mariculture" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Mariculture-Deluxe"

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "eclipse"
}

repositories {
    maven {
        name 'Overmind forge repo mirror'
        url 'https://gregtech.overminddl1.com/'
    }
    maven {
        name = "OpenComputers mirror for ChickenBones mods"
        url = "https://maven.cil.li/"
        metadataSources {
            mavenPom()
            artifact()
        }
    }
}

dependencies {
    compileOnly fileTree(dir: 'libs', include: '*.jar')

    compileOnly "codechicken:CodeChickenLib:${minecraft_version}-1.1.3.136:dev"
    compileOnly "codechicken:CodeChickenCore:${minecraft_version}-1.0.4.35:dev"
    compileOnly "codechicken:NotEnoughItems:${minecraft_version}-1.0.4.107:dev"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from('etc/mariculture') {
        include '**/*.txt'
        include '**/*.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
        exclude '**/*todolist.txt'
    }
}

runClient {
    jvmArgs '-Dfml.coreMods.load=mariculture.fishery.VanillaOverride'
}
runServer {
    jvmArgs '-Dfml.coreMods.load=mariculture.fishery.VanillaOverride'
}

jar {
    manifest {
        attributes 'FMLCorePlugin': 'mariculture.fishery.VanillaOverride'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}

task marinotechJar(type: Jar) {
    baseName = 'Mariculture-Base'
    manifest {
        attributes 'FMLCorePlugin': 'mariculture.fishery.VanillaOverride'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
    
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    
    from('etc/mariculture') {
        include '**/*.txt'
        include '**/*.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    
    from zipTree(jar.archivePath).matching {
        exclude 'maritech/**', 'assets/maritech/**'
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'src'
}

task deobfJar(type: Jar) {
    manifest {
        attributes 'FMLCorePlugin': 'mariculture.fishery.VanillaOverride'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
    from sourceSets.main.output
    classifier = 'dev'
}

marinotechJar.dependsOn('reobf')

artifacts {
    archives marinotechJar
    archives deobfJar
    archives sourceJar
}

// this is needed for IntelliJ so we don't have to copy over the assets manually every time
idea {
    module {
        inheritOutputDirs = true
    }
}
